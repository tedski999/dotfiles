#!/usr/bin/fish

# Notify user on changes to device networking
# Dependencies: NetworkManager

set id (random)

nmcli monitor | while read --line event
	if string match -q "Connectivity is now '*'" $event
		set state (echo $event | cut --delimiter \' --fields 2)
		set name (nmcli --terse --fields name --mode tabular connection show --active)
		switch $state
			case "none"
				notify-send -i network-wireless-disconnected -r $id "Network disconnected" "No internet access"
			case "portal"
				notify-send -i network-wireless-acquiring -r $id "Connected to $name" "Network behind captive portal"
			case "limited"
				notify-send -i network-wireless-acquiring -r $id "Connected to $name" "No internet access"
			case "full"
				set strength (nmcli --terse --fields ap.signal --mode tabular device show | head -1)
				set icon "network-wireless-connected-$(math "round($strength / 25) * 25")"
				notify-send -i $icon -r $id "Connected to $name" "Internet access available"
			case "unknown"
				notify-send -i network-wireless-acquiring -r $id "Unknown network status" "No internet access"
		end
	end
end
